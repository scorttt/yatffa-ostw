import "main.del";

playervar Number | String killStreak;
playervar Number | String bestStreak;
globalvar Number | String bestStreakServer;
globalvar Number | String bestStreakServerUser;
globalvar Number | String bestStreakServerHero;
playervar Number | String bestStreakHero;
playervar Boolean streak;

Color Rainbow(): CustomColor(128 + 127 * CosDeg(Time() * 150),
                             128 + 127 * CosDeg((Time() * 150) + 120),
                             128 + 127 * CosDeg((Time() * 150) + 240), 
                             250);

// Number percentage: (NormalizedHealth() * 100);
// Number hpR: (percentage <= 50) ? 255 : Floor(256 - (percentage - 50) * 5.12);
// Number hpG: (percentage >= 50) ? 255 : Floor(percentage * 5.12);
// Color HealthColor(): CustomColor(hpR, hpG, 0, 255);
Color AFKPulse(): CustomColor(Oscillate(750, 0, 255), Oscillate(750, 153, 255), 255, 255);
Color KickWarningPulse(): CustomColor(255, PingPong(1000, 255), 0, 255);

// String PrettyNumber(String number): 
//         StringLength(number) <= 3
//             ? number
//             : StringLength(number) <= 6
//                 ? StringSlice(number, 0, StringLength(number) - 3) + "," + StringSlice(number, StringLength(number) - 3, 3)
//                 : StringSlice(number, 0, StringLength(number) - 6) + "," + StringSlice(number, StringLength(number) - 6, 3) + "," + StringSlice(number, StringLength(number) - 3, 3);
//                 // breaks when a number is greater than 999,999,999

String PrettyNumber(String number): 
        StringLength(number) <= 3
            ? number
            : StringSlice(number, 0, StringLength(number) - 3) + "," + StringSlice(number, StringLength(number) - 3, 3);
                // breaks when a number is greater than 999,999

rule: "HUD : Global HUD : Welcome to Hell"
{
    if (!S_USE_OW_SCOREBORD)
    {
        # timer
        CreateHudText(
            VisibleTo:      All(), 
            Text:           MatchTime() >= 60 
                                ? "" 
                                : MatchTime() % 60,
            Subheader:      MatchTime() >= 60 
                                ? <"<0>:<1><2>", 
                                    Floor(MatchTime() / 60 % 60), 
                                    MatchTime() % 60 < 10 
                                        ? "0" 
                                        : "", 
                                    Floor(MatchTime() % 60)> 
                                : "",
            Location:       Location.Top, 
            SortOrder:      -10000, 
            TextColor:      CustomColor(255, PingPong(500, 255), 0, 255),
            SubheaderColor: MatchTime() <= S_MATCH_TIME / 4 
                                ? CustomColor(255, 255, PingPong(250, 255), 255)
                                : Color.White, 
            Reevaluation:   HudTextRev.VisibleToSortOrderStringAndColor, 
            Spectators:     Spectators.VisibleNever);
    }

    # warning
    CreateHudText(
        VisibleTo:      local, 
        Subheader:      <"<1> <0> <1>", warningStr[local.LangID], IconString(Icon.Warning)>,
        Location:       Location.Top, 
        SortOrder:      1, 
        SubheaderColor: Color.Yellow, 
        Reevaluation:   HudTextRev.VisibleToSortOrderStringAndColor, 
        Spectators:     Spectators.VisibleNever);

    if (S_AFK_ENABLED)
    {
        # afk text
        CreateInWorldText(
            VisibleTo:      local.afk ? local : null, 
            Header:         "AFK",
            Position:       RenderPosOf(0, 0.2), 
            Scale:          100,
            TextColor:      AFKPulse(),
            Reevaluation:   InworldTextRev.VisibleToPositionStringAndColor,
            Clipping:       Clipping.DoNotClip,
            Spectators:     Spectators.VisibleNever);

        # afk kick warning
        CreateInWorldText(
            VisibleTo:      local.afk ? local : null, 
            Header:         DuoStringReplace(playerAFKKickTimerStr[local.LangID], "{minutes}", "{seconds}",
                                Floor(local.afkTime % 3600 / 60) != 0 // if minute is not 0
                                    ? <"<0><1>", Floor(local.afkTime % 3600 / 60),
                                    Floor(local.afkTime % 3600 / 60) != 1 // if minute is not 1
                                        ? amountOfMinutesStr[local.LangID]
                                        : oneMinuteStr[local.LangID]>
                                    : "", // if minute is 0
                                <"<0><1>", Floor(local.afkTime % 60), 
                                    Floor(local.afkTime % 60) != 1 // if seconds arent 1
                                        ? amountOfSecondsStr[local.LangID]
                                        : oneSecondStr[local.LangID]>),
            Position:       RenderPosOf(0, -0.2), 
            Scale:          2,
            TextColor:      local.afkTime > S_AFK_KICK_TIME / 4 
                                ? CustomColor(Oscillate(100, 0, 255), Oscillate(100, 153, 255), 255, 255)
                                : KickWarningPulse(),
            Reevaluation:   InworldTextRev.VisibleToPositionStringAndColor,
            Clipping:       Clipping.DoNotClip,
            Spectators:     Spectators.VisibleNever);
    }

    if (S_MENU || S_DEF_HUD)
    {
        # hide/show menu
        CreateHudText(
            VisibleTo:      !local.afk ? local : null, 
            Subheader:      StringReplace(!local.menu ? holdToOpenMenuStr[local.LangID] : releaseToHideMenuStr[local.LangID], "{interact}", BindString(Button.Interact)),
            Location:       Location.Right, 
            SortOrder:      0.2, 
            SubheaderColor: local.hasOpenedMenu 
                                    ? Time() - local.userJoinTime < 3 && CustomColor(Oscillate(500, 0, 255), Oscillate(500, 153, 255), 255, 255) != CustomColor(255, 255, 255, 255)
                                        ? CustomColor(Oscillate(500, 0, 255), Oscillate(500, 153, 255), 255, 255)
                                        : Color.White
                                    : Color.White,
            Reevaluation:   HudTextRev.VisibleToStringAndColor,
            Spectators:     Spectators.VisibleNever);

        # double tap for favItem
        CreateHudText(
            VisibleTo:  !local.afk && local.favItem != Item.Invalid ? local : null, 
            Subheader:  local.favStatusIcon + "  " + DuoStringReplace(doubleTapToToggleFavStr[local.LangID], "{interact}", "{fav}", BindString(Button.Interact), local.favItemPStr), 
            Location:   Location.Right, 
            SortOrder:  0.3,
            Spectators: Spectators.VisibleNever);
    }
    if (S_INSTANT_RESPAWN)
    {
        # spacing for respawn hud
        CreateHudText(
            VisibleTo:  All(), 
            Text:       " \n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n", 
            Location:   Location.Top, 
            SortOrder:  100, 
            Spectators: Spectators.VisibleNever);

        # respawn hud
        CreateHudText(
            VisibleTo:      local.IsDead() ? local : null, 
            Subheader:      StringReplace(pressToRespawnInstantlyStr[local.LangID], "{interact}", BindString(Button.Interact)), 
            Location:       Location.Top, 
            SortOrder:      101,
            SubheaderColor: Color.Yellow, 
            Spectators:     Spectators.VisibleNever);

        CreateHudText(
            VisibleTo:      local.IsDead() ? local : null, 
            Subheader:      <"<0>\n", useNormalHeroSelectStr[local.LangID]>, 
            Location:       Location.Top, 
            SortOrder:      102,
            SubheaderColor: Color.Yellow, 
            Spectators:     Spectators.VisibleNever);
    }

    # player stats
    if (S_MODIFY_HUD || S_DEF_HUD)
    {
        # stats
        CreateHudText(
            VisibleTo: !local.afk && local.showStatsEnabled ? local : null, 
            Subheader: <" \n\n\n<0><1><2><3><4><5><6><7><8><9><10><11><12><13><14><15><16><17>", 

            // 0
            <"<0><1>\n", kdrStatStr[local.LangID], 
                Kills(local) > 0 && Deaths(local) == 0 
                    ? "∞" 
                    : (Kills(local) / Deaths(local)) + 0.0001>,

            // 1
            <"<0><1>\n", dmgRatioStatStr[local.LangID], 
                DamageDealt(local) > 0 && DamageTaken(local) == 0
                    ? "∞" 
                    : (DamageDealt(local) / DamageTaken(local)) + 0.0001>,

            <"<0><1>\n", dmgOutStatStr[local.LangID], PrettyNumber(<"<0>", DamageDealt(local)>)>, // 2
            <"<0><1>\n", dmgInStatStr[local.LangID], PrettyNumber(<"<0>", DamageTaken(local)>)>, // 3
            <"<0><1>%\n", accuracyStatStr[local.LangID], Accuracy(local)>, // 4

            CritAccuracy(local) != 0 // 5
                ? <"<0><1>%\n", critAccuracyStatStr[local.LangID], CritAccuracy(local)> : "",

            ScopedAccuracy(local) != 0 // 6
                ? <"<0><1>%\n", scopeAccuracyStatStr[local.LangID], ScopedAccuracy(local)> : "", 

            ScopedCritAccuracy(local) != 0// 7
                ? <"<0><1>%\n", scopeCritAccuracyStatStr[local.LangID], ScopedCritAccuracy(local)> : "",

            EnvironmentalKills(local) != 0 // 8
                ? <"<0><1>\n", environmentKillsStatStr[local.LangID], EnvironmentalKills(local)> : "",

            <"<0><1>\n", healingStatStr[local.LangID], PrettyNumber(<"<0>", Ceil(HealingTaken(local))>)>, // 9

            local.heroStatPStr.Length >= 1 && local.heroStatVal[0] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[0], PrettyNumber(<"<0>", local.heroStatVal[0]>), local.statMetric[0] != null ? local.statMetric[0] : ""> : "", // 10

            local.heroStatPStr.Length >= 2 && local.heroStatVal[1] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[1], PrettyNumber(<"<0>", local.heroStatVal[1]>), local.statMetric[1] != null ? local.statMetric[1] : ""> : "", // 11

            local.heroStatPStr.Length >= 3 && local.heroStatVal[2] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[2], PrettyNumber(<"<0>", local.heroStatVal[2]>), local.statMetric[2] != null ? local.statMetric[2] : ""> : "", // 12

            local.heroStatPStr.Length >= 4 && local.heroStatVal[3] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[3], PrettyNumber(<"<0>", local.heroStatVal[3]>), local.statMetric[3] != null ? local.statMetric[3] : ""> : "", // 13

            local.heroStatPStr.Length >= 5 && local.heroStatVal[4] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[4], PrettyNumber(<"<0>", local.heroStatVal[4]>), local.statMetric[4] != null ? local.statMetric[4] : ""> : "", // 14
            
            local.heroStatPStr.Length >= 6 && local.heroStatVal[5] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[5], PrettyNumber(<"<0>", local.heroStatVal[5]>), local.statMetric[5] != null ? local.statMetric[5] : ""> : "", // 15

            local.heroStatPStr.Length >= 7 && local.heroStatVal[6] != 0
                ? <"<0><1><2>\n", local.heroStatPStr[6], PrettyNumber(<"<0>", local.heroStatVal[6]>), local.statMetric[6] != null ? local.statMetric[6] : ""> : "", // 16

            local.bestStreak != 0 // 17
                ? <" \n<0>\n", StringReplace(bestKillStreakStatStr[local.LangID], "{number}", local.bestStreak)> : "">,

            Location: Location.Left, 
            SortOrder: 0.1, 
            SubheaderColor: Color.White, 
            Reevaluation: HudTextRev.VisibleToSortOrderAndString, 
            Spectators: Spectators.VisibleNever);
    }
}

rule: "HUD : Player Streak"
Event.OngoingPlayer
if (S_STREAKS_ENABLED)
if (S_MODIFY_HUD || S_DEF_HUD)
{
    # visible only to self
    CreateHudText(
        VisibleTo:      !afk && streak && showStreakEnabled ? player : null,

        Subheader:      <"<0>  <1><2>", HeroIconString(HeroOf()), player, youStr[local.LangID]>, 
        Text:           bestStreak != 0 
                            ? <"<0><1>/<2>", currentKillStreakStr[local.LangID], killStreak, bestStreak> 
                            : <"<0><1>", currentKillStreakStr[local.LangID], killStreak>, 
        Location:       Location.Left, 
        SortOrder:      1, 
        SubheaderColor: Rainbow(), 
        TextColor:      Color.White,
        Reevaluation:   HudTextRev.VisibleToSortOrderStringAndColor, 
        Spectators:     Spectators.VisibleNever);

    # visible to everyone excluding self
    CreateHudText(
        VisibleTo:      !afk && streak ? FilteredArray(All().Remove(player), Curr().showStreakEnabled) : null,

        Subheader:      <"<0>  <1>", HeroIconString(HeroOf()), player>, 
        Text:           bestStreak != 0 
                            ? <"<0><1>/<2>", currentKillStreakStr[local.LangID], killStreak, bestStreak> 
                            : <"<0><1>", currentKillStreakStr[local.LangID], killStreak>, 
        Location:       Location.Left, 
        SortOrder:      SlotOf() + 1, 
        SubheaderColor: Color.Team2, 
        TextColor:      Color.White, 
        Reevaluation:   HudTextRev.VisibleToSortOrderStringAndColor, 
        Spectators:     Spectators.VisibleNever);
}